<?xml version="1.0" encoding="UTF-8" standalone="no"?>
<event category="STEP" id="0">
  <actions>
    <action id="603" library="1">
      <!--action name: Code-->
      <kind>CODE</kind>
      <allowRelative>false</allowRelative>
      <question>false</question>
      <canApplyTo>true</canApplyTo>
      <actionType>CODE</actionType>
      <functionName/>
      <relative>false</relative>
      <not>false</not>
      <appliesTo>.self</appliesTo>
      <arguments>
        <argument kind="STRING">{
    var size, namelength, server;
    
    if(lobbysocket != -1) {
        if(socket_has_error(lobbysocket)) {
            if(numServers == -1) {
                show_message("Connecting to the lobby server has failed. Reason:#" + socket_error(lobbysocket));
            }
            socket_destroy(lobbysocket);
            lobbysocket = -1;
        } else if(numServers == -1) {
            if(tcp_receive(lobbysocket, 4)) {
                numServers = read_uint(lobbysocket);
            }
        } else if(serversRead &lt; numServers) {
            if(serverInfoLength == -1) {
                if(tcp_receive(lobbysocket, 4)) {
                    serverInfoLength = read_uint(lobbysocket);
                }
            }
            if(serverInfoLength != -1)
            {
                if(tcp_receive(lobbysocket, serverInfoLength))
                {
                    var protocol, port, infolen, playercount;
                    protocol = read_ubyte(lobbysocket);
                    port = read_ushort(lobbysocket);
                    if(protocol==0 and port&gt;0)
                    {
                        server = instance_create(0,0,Server);
                        server.port = port;
                        server.ip = string(read_ubyte(lobbysocket))+"."+string(read_ubyte(lobbysocket))+"."+string(read_ubyte(lobbysocket))+"."+string(read_ubyte(lobbysocket));
                        read_string(lobbysocket, 2+16);
                        server.slots = read_ushort(lobbysocket);
                        server.players = read_ushort(lobbysocket);
                        server.bots = read_ushort(lobbysocket);
                        server.private = ((read_ushort(lobbysocket)&amp;1) != 0);
                        server.compatible = false;
                        infolen = read_ushort(lobbysocket);
                        repeat(infolen)
                        {
                            var key, val;
                            key = read_string(lobbysocket, read_ubyte(lobbysocket));
                            if(key=="protocol_id")
                            {
                                buffer_set_readpos(global.protocolUuid, 0);
                                server.compatible = true;
                                repeat(read_ushort(lobbysocket))
                                    if(read_ubyte(global.protocolUuid) != read_ubyte(lobbysocket))
                                        server.compatible = false;
                            }
                            else
                            {
                                val = read_string(lobbysocket, read_ushort(lobbysocket));
                                ds_map_add(server.infos, key, val);
                            }
                        }
                        if(server.bots)
                            playercount = string(server.players) + "+" + string(server.bots);
                        else 
                            playercount = string(server.players);
                        server.playerstring =  playercount + "/" + string(server.slots);
                        
                        server.shortgame = "";
                        if(ds_map_exists(server.infos, "game_short"))
                            server.shortgame = ds_map_find_value(server.infos, "game_short");
                        else if(ds_map_exists(server.infos, "game"))
                            server.shortgame = ds_map_find_value(server.infos, "game");
                        if(server.shortgame != "")
                        {
                            if(ds_map_exists(server.infos, "game_ver"))
                                server.shortgame += " " + ds_map_find_value(server.infos, "game_ver");
                        }
                        server.name = ds_map_find_value(server.infos, "name");
                        server.name = string_replace(server.name, "#", "\#");
                        ds_list_sorted_insert(servers, server, compareServersByCompatAndName);
                        serversRead += 1;
                        serverInfoLength = -1;
                    }
                }
            }
        } else {
            socket_destroy(lobbysocket);
            lobbysocket = -1;
        }                    
    }
    
    if(mouse_x &gt; xbegin and mouse_x &lt; xbegin+width) {
        virtualitem = round((mouse_y-ybegin)/spacing-0.5);
        if(virtualitem&lt;0 or virtualitem&gt;items) {
            virtualitem = -1;
        } else {
            virtualitem += offset;
        }
        if(mouse_check_button_pressed(mb_left) and virtualitem&lt;ds_list_size(servers) and virtualitem&gt;=0){
            server = ds_list_find_value(servers, virtualitem);
            if(server.compatible)
            {            
                global.isHost = false;
                global.serverIP = server.ip;
                global.serverPort = server.port;
                if(instance_exists(Client))
                {   // We can't _actually_ destroy and recreate the Client here, because destroying it will cause a room change and that will cause the Create event not to run... Yay, GM!
                    with(Client)
                    {
                        event_perform(ev_destroy,0);
                        ClientCreate();
                    }
                }
                else
                {
                    instance_create(0,0,Client);
                }
                Client.returnRoom = Lobby;
            }
            else
            {
                var question;
                question = "The selected game is not compatible with this client.##";
                question += "Server: " + string_replace(ds_map_find_value(server.infos, "name"), "#", "\#")+"#";
                if(ds_map_exists(server.infos, "game"))
                    question += "Game/Mod: " + string_replace(ds_map_find_value(server.infos, "game"), "#", "\#")+"#";
                if(ds_map_exists(server.infos, "game_ver"))
                    question += "Version: " + string_replace(ds_map_find_value(server.infos, "game_ver"), "#", "\#")+"#";
                if(ds_map_exists(server.infos, "game_url"))
                {
                    var gameurl, reallyvisit;
                    gameurl = string_replace(ds_map_find_value(server.infos, "game_url"), "#", "\#");
                    question += "Website: " + gameurl + "##Do you want to visit this website now?";
                    if(show_question(question))
                        if(show_question("Warning: The website link is provided by the game server you selected and could lead anywhere.#Really visit '"+gameurl+"'?"))
                            action_splash_web(ds_map_find_value(server.infos, "game_url"), 1);

                }
                else
                {
                    show_message(question);
                }
            }
        }
    } else {
        virtualitem = -1;
    }
}
</argument>
      </arguments>
    </action>
  </actions>
</event>
